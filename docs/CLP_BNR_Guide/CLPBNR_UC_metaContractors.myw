#### Constructing Metalevel Contractors

Other implementations of CLP over intervals have been characterized (see [CLIP]) as either "RISC" or "CISC" designs. In the RISC approach each constraint is broken down into a sequence of low level primitives which are "executed" by the constraint engine (fixed point iterator). However, as seen in previous examples, narrowing of the internal fixed point iteration can be weak due to the dependency issue. CISC systems try to address this problem by treating the constraint as a single enity but this requires a more powerful solver. CLIP and CLP(BNR) are RISC designs; [Newton] and [Numerica] are CISC designs. To overcome some of the RISC limitations, CLIP used the concept of metalevel contractors which are a way of adding redundant constraints which can accelerate the convergence of the fixed point iteration. Relative to CISC designs, this has performance implications, but can be accomplished without complicating the underlying constraint infrastructure.

aside> Aside: With conventional floating point arithmetic, adding redundant equations must be done carefully because they can exacerbate problems caused by rounding errors. Using sound interval arithmetic, these problems are avoided and redundancy can be used to accelerate convergence. (See also [Algorithmic Power from Declarative Use of Redundant Constraints]).

A contractor is just a procedure which forces some narrowing operation on set of intervals. `solve`, `splitsolve` and `absolve`, as defined in the `clpBNR` package, are contractors bundled with an iterating algorithm that terminates when some condition is met, e.g., the width of the interval(s) is less than some value. Here's two contractors that split intervals at their midpoint; `mid_split_one` selects the largest interval (by width) from a list. It uses `mid_point_split` which takes a single interval as its argument and splits it (see [Appendix 1] for module `clpBNRmeta_contractor` source code):
.pl
	%
	% contractor to split largest interval of a list on midpoint
	%
	mid_split_one(Xs) :-
		select_split(Xs,X),  % select largest interval with largest width
		mid_split(X).        % split it
	%
	% contractor to split a single interval on midpoint if sufficiently wide (creates a choicepoint)
	%
	mid_split(X) :- small(X), !.    % too narrow to split
	mid_split(X) :-
		M is midpoint(X),
		({X=<M} ; {M=<X}).
	
	% select interval with largest width
	select_split([X],X) :- !.       % select last remaining element
	select_split([X1,X2|Xs],X) :-   % compare widths and discard one interval
		delta(X1,D1),
		delta(X2,D2),
		(D1 >= D2
		 -> select_split([X1|Xs],X)
		 ;  select_split([X2|Xs],X)
		).
 If the selected interval is already small (as defined by `clpBNR:small/1`), the contractor has no effect. If this isn't the case, the interval is split at its midpoint leaving a choicepoint so the alternatives can be explored on backtracking. If further splitting is required, an iterator becomes necessary to keep the process going. Here's a simple one which  executes a `Goal` a maximum of `N` times (`N>0`) or until `Test` succeeds:
.pl
	%
	% General purpose iterator: execute Goal a maximum of N times or until Test succeeds
	%
	iterate_until(N,Test,Goal) :- N>0, !, 
		Goal,
		N1 is N-1,
		(Test
		 -> true
		  ; iterate_until(N1,Test,Goal)
		).
	iterate_until(_N,_,_).  % non-positive N --> exit
A few simple examples from [Solving Polynomial Equations in one Variable] using the `mid_split_one` contractor with this iterator:
eg
	﻿?- X::real, {X**4-4*X**3+4*X**2-4*X+3==0}, iterate_until(50,small(X),mid_split_one([X])).
	X:: 1.00000... ;
	X:: 3.00000... ;
	false.
	
	﻿?- {X**2+Y**2==2, Y-X**2==0}, iterate_until(50,small([X,Y]),mid_split_one([X,Y])).
	X:: -1.00000000000000...,
	Y:: 1.00000000000000... ;
	X:: 1.00000000000000...,
	Y:: 1.00000000000000... .
This demonstrates the general principle of constructing metalevel contractors, but the real value comes when the functionality of built-in CISC style contractors can be replicated as metalevel contractors.

##### A Multivariate Taylor Contractor

The Taylor series expansion of a function can be used to define redundant constraints which can accelerate the convergence of the fixed point iteration. For a multivariate equation of the form 
math>  f(x_1,...x_i,...x_n) = 0
a redundant constraint of the form
math>  0 = f(a_i)+f'(xi_i)*(x_i-a_i) , xi_i = a_i+t_i*(x_i-a_i) , 0<=t_i<=1
can be added for each {`i`}, where {`a_i`} is some value in the domain of {`x_i`}. Usually the best convergence occurs when {`a_i`} is the midpoint of {`x_i`}. (For a more complete description of the underlying math see [CLIP].)

We'll now define a predicate which takes an equation of the form `E1 == E2` and builds a centred form contractor `cf_contractor(Xs,As)` which can be used in an iterator in much the same way as `mid_split` in the example above. In the process, the original equation (as a constraint) and any redundant constraints are added to the system. There is a requirement that the expression `E1-E2` be differentiable over the range of the variables it contains. The `Xs` and `As` correspond to the {`x_i`}'s and {`a_i`}'s in the equations defined above.
.pl
	build_taylor_contractor(E1==E2, cf_contractor(Xs,As)) :-
		Exp=E1-E2,                           % define constraints for Exp==0
		term_variables(Exp,Xs),              % constraint arguments 
		make_EQ_(Exp,TEQ),                   % original constraint with arguments
		% build constraint list starting with Z's and ending with TEQ and DEQ ()
		T::real(0,1),
		make_As_and_Zs_(Xs,T,As,Zs,Cs,[TEQ,DEQ]),
		% now build Taylor constraint, DEQ
		copy_term_nat(Exp,AExp),             % copy of original constraint with As
		term_variables(AExp,As),
		sum_diffs(Xs, As, Zs, Zs, Exp, AExp, DEQ),  % add on D(Z)'s'
		% ensure that any vars in original equation are (at least) finite real intervals
		Xs::real,
		{Cs}.                                % apply constraints
	
	make_As_and_Zs_([],T,[],[],Tail,Tail).
	make_As_and_Zs_([X|Xs],T,[A|As],[Z|Zs],[Z==A+T*(X-A)|CZs],Tail) :-
		make_As_and_Zs_(Xs,T,As,Zs,CZs,Tail).
	
	sum_diffs([], [], [], AllZs, Exp, ExpIn, EQ) :- make_EQ_(ExpIn,EQ).
	sum_diffs([X|Xs], [A|As], [Z|Zs], AllZs, Exp, AExp, DEQ) :-
		copy_term_nat(Exp,NExp),             % copy expression and replace Xs by Zs
		term_variables(NExp,AllZs),
		partial_derivative(NExp,Z,DZ),       % differentiate wrt. Z and add to generated expression
		sum_diffs(Xs, As, Zs, AllZs, Exp, AExp+DZ*(X-A), DEQ).
	
	% map expression Exp to an equation equivalent to Exp==0 with numeric RHS
	make_EQ_(Exp,LHS==RHS) :-    % turn expression into equation equivalent to Exp==0.
		make_EQ_(Exp,LHS,RHS).
	
	make_EQ_(E,E,0)         :- var(E), !.
	make_EQ_(X+Y,X,SY)      :- number(Y), !, SY is -Y.
	make_EQ_(X-Y,X,Y)       :- number(Y), !.
	make_EQ_(X+Y,Y,SX)      :- number(X), !, SX is -X.
	make_EQ_(X-Y,SY,SX)     :- number(X), !, SX is -X, negate_sum_(Y,SY).
	make_EQ_(X+Y,LHS+Y,RHS) :- !, make_EQ_(X,LHS,RHS).
	make_EQ_(X-Y,LHS-Y,RHS) :- !, make_EQ_(X,LHS,RHS).
	make_EQ_(E,E,0).        % default (non +/- subexpression)
	
	negate_sum_(Y,-Y) :- var(Y), !.
	negate_sum_(X+Y,NX-Y) :- !, negate_sum_(X,NX).
	negate_sum_(X-Y,NX+Y) :- !, negate_sum_(X,NX).
	negate_sum_(E,-E).
After building the contractor the constraints are activated (last step in `build_taylor_contractor`) but the actual contraction operation has not yet been implemented. (`build_taylor_contractor/2` just builds the "goal" to be called.) For the Taylor contractor, this operation must bind the values of {`a_i`} (i.e., the members of `As`) to the midpoints of {`x_i`}. However, to support repetitive application of the contractor (required by the iterator), the contractor should not permanently bind any {`a_i`}. `findall/3` will be used to achieve this "forward checking" (as suggested in [CLIP]):
.pl
	cf_contractor(Xs,As) :-
		findall(Ds,(maplist(bind_to_midpoint,Xs,As),maplist(domain,Xs,Ds)),[XDs]),
		maplist(::,Xs,XDs).
	
	bind_to_midpoint(X,A) :- A is float(midpoint(X)).
After the call to `findall`, `XDs` is a list of narrowed domains of `Xs` and are then applied to `Xs`.  But the `As` are unchanged so they can be used again in the next iteration with the updated `Xs` values.
aside> Aside: This contractor can be used with any "centred form", e.g., Newton or Krawczyk, since it only depends on intervals and their midpoints, hence its name `cf_contractor`. The details which distinguish the variety of centred form are built into the variables' constraints.
This contractor can be used as an alternative to `solve` and absolve` to find roots of polynomial equations (as done in [Solving Polynomial Equations in one Variable]). In general, it provides better results (narrower intervals and fewer "false positives") in a fraction of the time: 
eg
	﻿?- X::real,build_taylor_contractor(X**2-4*X+3==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: 1.00000000000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 3.00000000000000...,
	_A::real(-1.0Inf, 1.0Inf).
	
	﻿?- X::real,build_taylor_contractor(X**4-4*X**3+4*X**2-4*X+3==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: 1.000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 3.00000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	false.
	
	﻿?- X::real,build_taylor_contractor(17*X**256+35*X**17-99*X==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: 0.0000000000000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 1.00502789289401...,
	_A::real(-1.0Inf, 1.0Inf).
	
	﻿?- X::real,build_taylor_contractor(35*X**256-14*X**17+X==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: -0.847943660827315...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 0.0000000000000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 0.847943660827315...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 0.995842494200498...,
	_A::real(-1.0Inf, 1.0Inf).
	
	﻿?- X::real,build_taylor_contractor(X**4-12*X**3+47*X**2-60*X==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: 0.0000000000000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 3.00000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 4.00000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 5.0000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	false.
	
	﻿?- X::real,build_taylor_contractor(X**2-2*X+1==0,T),iterate_until(20,small(X),(mid_split_one([X]),T)).
	T = cf_contractor([X], [_A]),
	X:: 0.999999...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 1.000000...,
	_A::real(-1.0Inf, 1.0Inf) ;
	T = cf_contractor([X], [_A]),
	X:: 1.0000001...,
	_A::real(-1.0Inf, 1.0Inf) ;
	false.
Comparing `solve` and iteration using the metalevel Taylor contractor in terms of narrowing operations performed (should be relatively implementation and machine independent):
.myw
	.tsv
		Polynomial						`solve`			Taylor
		`X**2-4*X+3`					`    776`		`  1327`
		`X**4-4*X**3+4*X**2-4*X+3`		`  22056`		` 16070`
		`17*X**256+35*X**17-99*X`		`    226`		`   214`
		`35*X**256-14*X**17+X`			`   2539`		`   835`
		`X**4-12*X**3+47*X**2-60*X`		` 350324`		` 19496`
		`X**2-2*X+1`					`1998181`		`141451`
	&	// local styles for table
		@css
			table.my_array { border-collapse:collapse; }
			table.my_array tr td { background:whitesmoke; padding: 4px 16px; }
			table.my_array tr:nth-child(1) td { background:lightgray; }

Other than the simple cases where minimal splitting is required, iteration using the Taylor contractor is significantly faster (sometimes by a factor of 15) and does a better job at eliminating false positives due to precision limits. (Multiple roots at one value are usually problematical; as an exercise try `X**4-X**3-3*X**2+5*X-2` which has a root at `X= -2` and a triple root at `X=1`.)

Problems involving multiple variables typically require more than one equation. Since the variables are shared, the generated Taylor contractors (one per equation) can be merged into a single contractor by omitting duplicates. `build_taylor_merged/2` takes a list of equations and constructs a single `cf_contractor` for use in an iterator:
.pl
	build_taylor_merged(Es,T) :-
		maplist(build_taylor_contractor_,Es,Ts),  % construct list of contractors
		cf_merge(Ts,T).                           % and merge them
	
	cf_merge(CFs,CF) :- cf_merge(CFs,cf_contractor([],[]),CF).
	
	cf_merge([],CF,CF).
	cf_merge([cf_contractor(Xs,As)|CFs],cf_contractor(XsIn,AsIn),CF) :-
		cf_add(Xs,As,XsIn,AsIn,XsOut,AsOut),
		cf_merge(CFs,cf_contractor(XsOut,AsOut),CF).
	
	cf_add([],[],Xs,As,Xs,As).
	cf_add([X|Xs],[A|As],XsIn,AsIn,XsOut,AsOut) :-
		var_existing(XsIn,AsIn,X,A), !,
		cf_add(Xs,As,XsIn,AsIn,XsOut,AsOut).
	cf_add([X|Xs],[A|As],XsIn,AsIn,XsOut,AsOut) :-
		cf_add(Xs,As,[X|XsIn],[A|AsIn],XsOut,AsOut).
	
	var_existing([Xex|_], [A|_],   X,A) :- Xex==X, !.
	var_existing([_|XsIn],[_|AsIn],X,A) :- var_existing(XsIn,AsIn,X,A).
Examples of using this predicate follow.

##### The Numerica "Combustion" Problem

An example problem from [Numerica] from (taken from A. P. MORGAN, Solving Polynomial Systems Using Continuation for Scientic and Engineering Problems, Prentice-Hall, 1987) refers to a combustion problem for a temperature of 3000˚ represented by a system of 9 equations in 9 unknowns. Expressed in Prolog it looks like:
.pl
	combustion(
		[X1,X2,X3,X4,X5,X6,X7,X8,X9,X10],
		[X2+2*X6+X9+2*X10 == 1e-5,
		 X3+X8 == 3e-5,
		 X1+X3+2*X5+2*X8+X9+X10 == 5e-5,
		 X4+2*X7 == 1e-5,
		 0.5140437e-7*X5 == X1**2,
		 0.1006932e-6*X6 == 2*X2**2,
		 0.7816278e-15*X7 == X4**2,
		 0.1496236e-6*X8 == X1*X3,
		 0.6194411e-7*X9 == X1*X2,
		 0.2089296e-14*X10 == X1*X2**2
		]
		).
Because there is only a single occurrence of any variable in any equation, the fixed point iteration forces considerable narrowing by itself:
eg
	﻿?- combustion(Vs,Es),Vs::real(0,1),{Es}.
	Vs = [_A, _B, _C, _D, _E, _F, _G, _H, _I, _J],
	Es = [_B+2*_F+_I+2*_J==1.0e-5, _C+_H==3.0e-5, _A+_C+2*_E+2*_H+_I+_J==5.0e-5, _D+2*_G==1.0e-5, 5.140437e-8*_E==_A**2, 1.006932e-7*_F==2*_B**2, 7.816278e-16*_G==_D**2, 1.496236e-7*_H==_A*_C, 6.194411e-8*_I==_A*_B, 2.089296e-15*_J==_A*_B**2],
	_A:: 0.0000001...,
	_B:: 0.000000...,
	_C:: 0.00001...,
	_D:: 0.0000000000625149...,
	_E:: 0.000000...,
	_F:: 0.000001...,
	_G:: 0.0000049999687425...,
	_H:: 0.00001...,
	_I:: 0.000000...,
	_J:: 0.000003... .
Adding `solve` further refines the answer:
eg
	﻿?- combustion(Vs,Es),Vs::real(0,1),{Es},solve(Vs).
	Vs = [_A, _B, _C, _D, _E, _F, _G, _H, _I, _J],
	Es = [_B+2*_F+_I+2*_J==1.0e-5, _C+_H==3.0e-5, _A+_C+2*_E+2*_H+_I+_J==5.0e-5, _D+2*_G==1.0e-5, 5.140437e-8*_E==_A**2, 1.006932e-7*_F==2*_B**2, 7.816278e-16*_G==_D**2, 1.496236e-7*_H==_A*_C, 6.194411e-8*_I==_A*_B, 2.089296e-15*_J==_A*_B**2],
	_A:: 0.000000147...,
	_B:: 0.000000226...,
	_C:: 0.0000151...,
	_D:: 0.0000000000625149...,
	_E:: 0.000000421...,
	_F:: 0.00000101...,
	_G:: 0.0000049999687425...,
	_H:: 0.00001487...,
	_I:: 0.00000053...,
	_J:: 0.00000360... .
The same problem using metalevel contractors:
eg 
	﻿?- combustion(Vs,Es),Vs::real(0,1),build_taylor_merged(Es,T),
	iterate_until(50,small(Vs),(T,mid_split_one(Vs))).
	Vs = [_A, _B, _C, _D, _E, _F, _G, _H, _I, _J],
	Es = [_B+2*_F+_I+2*_J==1.0e-5, _C+_H==3.0e-5, _A+_C+2*_E+2*_H+_I+_J==5.0e-5, _D+2*_G==1.0e-5, 5.140437e-8*_E==_A**2, 1.006932e-7*_F==2*_B**2, 7.816278e-16*_G==_D**2, 1.496236e-7*_H==_A*_C, 6.194411e-8*_I==_A*_B, 2.089296e-15*_J==_A*_B**2],
	T = cf_contractor([_G, _D, _E, _A, _H, _C, _J, _I, _F, _B], [_K, _L, _M, _N, _O, _P, _Q, _R, _S, _T]),
	_A:: 0.0000001...,
	_B:: 0.0000002...,
	_C:: 0.000015...,
	_D:: 0.0000000000625149...,
	_E:: 0.0000004...,
	_F:: 0.000001...,
	_G:: 0.0000049999687425...,
	_H:: 0.000015...,
	_I:: 0.000000...,
	_J:: 0.000003...,
	_T::real(-1.0Inf, 1.0Inf),
	_S::real(-1.0Inf, 1.0Inf),
	_R::real(-1.0Inf, 1.0Inf),
	_Q::real(-1.0Inf, 1.0Inf),
	_P::real(-1.0Inf, 1.0Inf),
	_O::real(-1.0Inf, 1.0Inf),
	_N::real(-1.0Inf, 1.0Inf),
	_M::real(-1.0Inf, 1.0Inf),
	_L::real(-1.0Inf, 1.0Inf),
	_K::real(-1.0Inf, 1.0Inf) ;
	false.
This halts when the default precision limit is reached (`clpBNR_default_precision= 6`). To achieve the same precision as `solve`, it is necessary to tune the test via the second argument to `small`,to get the same precision:
eg
	﻿?- combustion(Vs,Es),Vs::real(0,1),build_taylor_merged(Es,T),
	iterate_until(50,small(Vs,9),(T,mid_split_one(Vs))).
	Vs = [_A, _B, _C, _D, _E, _F, _G, _H, _I, _J],
	Es = [_B+2*_F+_I+2*_J==1.0e-5, _C+_H==3.0e-5, _A+_C+2*_E+2*_H+_I+_J==5.0e-5, _D+2*_G==1.0e-5, 5.140437e-8*_E==_A**2, 1.006932e-7*_F==2*_B**2, 7.816278e-16*_G==_D**2, 1.496236e-7*_H==_A*_C, 6.194411e-8*_I==_A*_B, 2.089296e-15*_J==_A*_B**2],
	T = cf_contractor([_G, _D, _E, _A, _H, _C, _J, _I, _F, _B], [_K, _L, _M, _N, _O, _P, _Q, _R, _S, _T]),
	_A:: 0.0000001470...,
	_B:: 0.0000002262...,
	_C:: 0.000015128...,
	_D:: 0.0000000000625149...,
	_E:: 0.0000004209...,
	_F:: 0.000001016...,
	_G:: 0.0000049999687425...,
	_H:: 0.000014872...,
	_I:: 0.000000537...,
	_J:: 0.000003602...,
	_T::real(-1.0Inf, 1.0Inf),
	_S::real(-1.0Inf, 1.0Inf),
	_R::real(-1.0Inf, 1.0Inf),
	_Q::real(-1.0Inf, 1.0Inf),
	_P::real(-1.0Inf, 1.0Inf),
	_O::real(-1.0Inf, 1.0Inf),
	_N::real(-1.0Inf, 1.0Inf),
	_M::real(-1.0Inf, 1.0Inf),
	_L::real(-1.0Inf, 1.0Inf),
	_K::real(-1.0Inf, 1.0Inf) ;
	false.
The builtin `solve` and fixed point iteration does a credible job on this problem so it turns out adding the metalevel contractor redundant constraints is not worth the additional overhead incurred. It is often the case some experimentation is required to achieve required precision within a given time budget.

##### The Broyden Banded Function

A common example in interval arithmetic publications is the Broyden banded function which defines the following system of {`n`} equations in {`n`} variables {`{x_1,...,x_n}`}:
math> x_i*(2+5x_i^2) = sum_(i-5<=j<=i+1,j!=i,jin[1,n])x_j*(x_j+1)
A predicate to generate this set of equations for different values of `N`:
.pl
	broyden(N,Vs,EQs) :-
		length(Vs,N),
		cbbf(1,N,Vs,Vs,EQs), !.
	
	cbbf(I,N,[Xi|Xs],L,[Xi*(2+5*Xi**2) + 1==Si|Rest]) :- 
		make_si(1,I,N,L,0,Si),
		I1 is I+1,
		cbbf(I1,N,Xs,L,Rest).
	cbbf(_I,_N,[],_,[]).
	
	make_si(J,_,N,_,S,S)         :- J > N , !.                       % terminate
	make_si(J,I,N,[Xj|Xs],S1,S)  :- I-5 =< J, J =< I+1, I\==J,  !,   % add a term
	  J1 is J+1,
	  make_si(J1,I,N,Xs,S1 + Xj*(1+Xj),S).
	make_si(J,I,N,[_Xj|Xs],S1,S) :-                                  % skip
	  J1 is J+1,
	  make_si(J1,I,N,Xs,S1,S).
Using `solve` will generate the correct result; for `N=5`:
eg
	﻿?- broyden(5,Vs,Es),Vs::real(-1,1),{Es},solve(Vs).
	Vs = [_A, _B, _C, _D, _E],
	Es = [_A*(2+5*_A**2)+1==0+_B*(1+_B), _B*(2+5*_B**2)+1==0+_A*(1+_A)+_C*(1+_C), _C*(2+5*_C**2)+1==0+_A*(1+_A)+_B*(1+_B)+_D*(1+_D), _D*(2+5*_D**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_E*(1+_E), _E*(2+5*_E**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)],
	_A:: -0.4283028...,
	_B:: -0.4765965...,
	_C:: -0.5196377...,
	_D:: -0.5588619...,
	_E:: -0.558862... ;
	false.
If the Taylor contractor is used by itself, the result is less than satisfactory:
eg
	﻿?- broyden(5,Vs,Es),Vs::real(-1,1),build_taylor_merged(Es,T),
	iterate_until(50,small(Vs),T).
	Vs = [_A, _B, _C, _D, _E],
	Es = [_A*(2+5*_A**2)+1==0+_B*(1+_B), _B*(2+5*_B**2)+1==0+_A*(1+_A)+_C*(1+_C), _C*(2+5*_C**2)+1==0+_A*(1+_A)+_B*(1+_B)+_D*(1+_D), _D*(2+5*_D**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_E*(1+_E), _E*(2+5*_E**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)],
	T = cf_contractor([_E, _D, _C, _B, _A], [_F, _G, _H, _I, _J]),
	_A::real(-0.8986026814821334, 0.8192591229206059),
	_B::real(-0.9895306792624361, 0.9729979350338642),
	_C::real(-1, 1),
	_D::real(-1, 1),
	_E::real(-1, 1),
	_J::real(-1.0Inf, 1.0Inf),
	_I::real(-1.0Inf, 1.0Inf),
	_H::real(-1.0Inf, 1.0Inf),
	_G::real(-1.0Inf, 1.0Inf),
	_F::real(-1.0Inf, 1.0Inf).
The problem is that the redundant constraints of centred form contractors become less effective when the intervals are wider, as we see here when the initial domains are reduced to `real(-1,0)`:
eg
	﻿?- broyden(5,Vs,Es),Vs::real(-1,0),build_taylor_merged(Es,T),
	iterate_until(50,small(Vs),T).
	Vs = [_A, _B, _C, _D, _E],
	Es = [_A*(2+5*_A**2)+1==0+_B*(1+_B), _B*(2+5*_B**2)+1==0+_A*(1+_A)+_C*(1+_C), _C*(2+5*_C**2)+1==0+_A*(1+_A)+_B*(1+_B)+_D*(1+_D), _D*(2+5*_D**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_E*(1+_E), _E*(2+5*_E**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)],
	T = cf_contractor([_E, _D, _C, _B, _A], [_F, _G, _H, _I, _J]),
	_A:: -0.428302864642701...,
	_B:: -0.476596531501095...,
	_C:: -0.51963772210075...,
	_D:: -0.55886195652702...,
	_E:: -0.55886195652702...,
	_J::real(-1.0Inf, 1.0Inf),
	_I::real(-1.0Inf, 1.0Inf),
	_H::real(-1.0Inf, 1.0Inf),
	_G::real(-1.0Inf, 1.0Inf),
	_F::real(-1.0Inf, 1.0Inf).
When the initial domains can't be predicted so precisely, the `mid_split_one` contactor can be used to sub-divide the solution space; the redundant constraints will often quickly prune the search space:
eg
	﻿?- broyden(5,Vs,Es),Vs::real(-1,1),build_taylor_merged(Es,T),
	iterate_until(50,small(Vs),(T,mid_split_one(Vs))).
	Vs = [_A, _B, _C, _D, _E],
	Es = [_A*(2+5*_A**2)+1==0+_B*(1+_B), _B*(2+5*_B**2)+1==0+_A*(1+_A)+_C*(1+_C), _C*(2+5*_C**2)+1==0+_A*(1+_A)+_B*(1+_B)+_D*(1+_D), _D*(2+5*_D**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_E*(1+_E), _E*(2+5*_E**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)],
	T = cf_contractor([_E, _D, _C, _B, _A], [_F, _G, _H, _I, _J]),
	_A:: -0.42830286464270...,
	_B:: -0.4765965315011...,
	_C:: -0.519637722100...,
	_D:: -0.55886195652...,
	_E:: -0.55886195652...,
	_J::real(-1.0Inf, 1.0Inf),
	_I::real(-1.0Inf, 1.0Inf),
	_H::real(-1.0Inf, 1.0Inf),
	_G::real(-1.0Inf, 1.0Inf),
	_F::real(-1.0Inf, 1.0Inf) ;
	false.
Comparing performance, using the Taylor contractor produces a more precise answer than `solve` in a fraction of the time. (For `N = 40`, the Taylor version runs about 35 times faster.) Furthermore, it scales (approximately) linearly with `N` and is fairly insensitive to the initial range: 
eg
	﻿?- broyden(20,Vs,Es),Vs::real,build_taylor_merged(Es,T),
	iterate_until(50,small(Vs),(T,mid_split_one(Vs))).
	Vs = [_A, _B, _C, _D, _E, _F, _G, _H, _I, _J, _K, _L, _M, _N, _O, _P, _Q, _R, _S, _T],
	Es = [_A*(2+5*_A**2)+1==0+_B*(1+_B), _B*(2+5*_B**2)+1==0+_A*(1+_A)+_C*(1+_C), _C*(2+5*_C**2)+1==0+_A*(1+_A)+_B*(1+_B)+_D*(1+_D), _D*(2+5*_D**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_E*(1+_E), _E*(2+5*_E**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)+_F*(1+_F), _F*(2+5*_F**2)+1==0+_A*(1+_A)+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)+_E*(1+_E)+_G*(1+_G), _G*(2+5*_G**2)+1==0+_B*(1+_B)+_C*(1+_C)+_D*(1+_D)+_E*(1+_E)+_F*(1+_F)+_H*(1+_H), _H*(2+5*_H**2)+1==0+_C*(1+_C)+_D*(1+_D)+_E*(1+_E)+_F*(1+_F)+_G*(1+_G)+_I*(1+_I), _I*(2+5*_I**2)+1==0+_D*(1+_D)+_E*(1+_E)+_F*(1+_F)+_G*(1+_G)+_H*(1+_H)+_J*(1+_J), _J*(2+5*_J**2)+1==0+_E*(1+_E)+_F*(1+_F)+_G*(1+_G)+_H*(1+_H)+_I*(1+_I)+_K*(1+_K), _K*(2+5*_K**2)+1==0+_F*(1+_F)+_G*(1+_G)+_H*(1+_H)+_I*(1+_I)+_J*(1+_J)+_L*(1+_L), _L*(2+5*_L**2)+1==0+_G*(1+_G)+_H*(1+_H)+_I*(1+_I)+_J*(1+_J)+_K*(1+_K)+_M*(1+_M), _M*(2+5*_M**2)+1==0+_H*(1+_H)+_I*(1+_I)+_J*(1+_J)+_K*(1+_K)+_L*(1+_L)+_N*(1+_N), _N*(2+5*_N**2)+1==0+_I*(1+_I)+_J*(1+_J)+_K*(1+_K)+_L*(1+_L)+_M*(1+_M)+_O*(1+_O), _O*(2+5*_O**2)+1==0+_J*(... + ...)+_K*(1+_K)+_L*(1+_L)+_M*(1+_M)+_N*(1+_N)+_P*(1+_P), _P*(2+5*_P**2)+1==0+ ... * ... + _L*(... + ...)+_M*(1+_M)+_N*(1+_N)+_O*(1+_O)+_Q*(1+_Q), _Q*(2+5*_Q**2)+1== ... + ... + ... * ... + _N*(... + ...)+_O*(1+_O)+_P*(1+_P)+_R*(1+_R), _R*(2+5* ... ** ...)+1== ... + ... + ... * ... + _P*(... + ...)+_Q*(1+_Q)+_S*(1+_S), _S*(2+ ... * ...)+1== ... + ... + ... * ... + _R*(... + ...)+_T*(1+_T), _T*(... + ...)+1== ... + ... + ... * ... + _S*(... + ...)],
	T = cf_contractor([_T, _S, _R, _Q, _P, _O, _N, _M, _L, _K, _J, _I, _H, _G, _F, _E, _D, _C, _B, _A], [_U, _V, _W, _X, _Y, _Z, _A1, _B1, _C1, _D1, _E1, _F1, _G1, _H1, _I1, _J1, _K1, _L1, _M1, _N1]),
	_A:: -0.428302863587250...,
	_B:: -0.47659642435629...,
	_C:: -0.51965246364640...,
	_D:: -0.55809932485615...,
	_E:: -0.59250615596508...,
	_F:: -0.62450370741051...,
	_G:: -0.62323866913245...,
	_H:: -0.62141967671364...,
	_I:: -0.61961584283347...,
	_J:: -0.6182260179198...,
	_K:: -0.6175180248415...,
	_L:: -0.617731830318...,
	_M:: -0.61790031625...,
	_N:: -0.6180077985...,
	_O:: -0.618057061...,
	_P:: -0.61806270...,
	_Q:: -0.6180472...,
	_R:: -0.6180112...,
	_S:: -0.6188720...,
	_T:: -0.5862769...,
	_N1::real(-1.0Inf, 1.0Inf),
	_M1::real(-1.0Inf, 1.0Inf),
	_L1::real(-1.0Inf, 1.0Inf),
	_K1::real(-1.0Inf, 1.0Inf),
	_J1::real(-1.0Inf, 1.0Inf),
	_I1::real(-1.0Inf, 1.0Inf),
	_H1::real(-1.0Inf, 1.0Inf),
	_G1::real(-1.0Inf, 1.0Inf),
	_F1::real(-1.0Inf, 1.0Inf),
	_E1::real(-1.0Inf, 1.0Inf),
	_D1::real(-1.0Inf, 1.0Inf),
	_C1::real(-1.0Inf, 1.0Inf),
	_B1::real(-1.0Inf, 1.0Inf),
	_A1::real(-1.0Inf, 1.0Inf),
	_Z::real(-1.0Inf, 1.0Inf),
	_Y::real(-1.0Inf, 1.0Inf),
	_X::real(-1.0Inf, 1.0Inf),
	_W::real(-1.0Inf, 1.0Inf),
	_V::real(-1.0Inf, 1.0Inf),
	_U::real(-1.0Inf, 1.0Inf) .

##### Ebers and Moll Transistor Model

One of the more challenging examples in the literature is the Ebers-Moll transistor model consisting of 9 non-linear equations in 9 variables. From [Progress in the solving of a circuit design problem]:
>	"*Ebers and Moll’s circuit design problem [6] is considered as a challenging benchmark for local and global methods. Recently, two interval-based techniques were successfully applied: Ratschek and Rokne’ algorithm [17] has derived the solution with a precision of four significant digits in about fourteen months using a network of thirty Sun Sparc 1 workstations; Puget and Van Hentenryck’ method [16] has solved the problem with a precision of eight significant digits in about forty minutes using a Sun Sparc UltraII workstation (the improvement is great though different machines were used).*"
The quoted paper reports further algorithmic improvements resulting in 12 digit accuracy in less than 10 minutes. While `clpBNR`, being a wholly Prolog implementation, is not at all competitive on performance, this a good example of what issues can arise given such a "challenging" problem.

The Prolog definition of the Ebers and Moll transistor example:
.pl
	ebers_moll(Vs,Es) :-
		Vs=[X1,X2,X3,X4,X5,X6,X7,X8,X9],
		G1s=g1(0.485,0.752,0.869,0.982),
		G2s=g2(0.369,1.254,0.703,1.455),
		G3s=g3(5.2095,10.0677,22.9274,20.2153),
		G4s=g4(23.3037,101.779,111.461,191.267),
		G5s=g5(28.5132,111.8468,134.3844,211.4823),
		eq1s(Vs,G1s,G2s,G3s,G4s,G5s,Eq1s),
		eq2s(Vs,G1s,G2s,G3s,G4s,G5s,Eq2s),
		flatten([Eq1s,Eq2s,X1*X3-X2*X4==0],Es).
		 
	eq1s(Xs,G1s,G2s,G3s,G4s,G5s,Eqs) :- eq1s(4,Xs,G1s,G2s,G3s,G4s,G5s,Eqs).
	eq1s(0,Xs,G1s,G2s,G3s,G4s,G5s,[]) :-!.
	eq1s(K,Xs,G1s,G2s,G3s,G4s,G5s,[EqK|Eqs]) :-
		K1 is K-1, eq1s(K1,Xs,G1s,G2s,G3s,G4s,G5s,Eqs),
		eq1(K,Xs,G1s,G2s,G3s,G4s,G5s,EqK).
	
	eq2s(Xs,G1s,G2s,G3s,G4s,G5s,Eqs) :- eq2s(4,Xs,G1s,G2s,G3s,G4s,G5s,Eqs).
	eq2s(0,Xs,G1s,G2s,G3s,G4s,G5s,[]) :-!.
	eq2s(K,Xs,G1s,G2s,G3s,G4s,G5s,[EqK|Eqs]) :-
		K1 is K-1, eq2s(K1,Xs,G1s,G2s,G3s,G4s,G5s,Eqs),
		eq2(K,Xs,G1s,G2s,G3s,G4s,G5s,EqK).
		
	eq1(K,[X1,X2,X3,X4,X5,X6,X7,X8,X9|Ys],G1s,G2s,G3s,G4s,G5s,
		(1-X1*X2)*X3*(exp(X5*(G1K-G3K*X7*1e-3-G5K*X8*1e-3))-1)-G5K+G4K*X2 == 0
		) :-
		arg(K,G1s,G1K), arg(K,G3s,G3K), arg(K,G4s,G4K), arg(K,G5s,G5K).
	
	eq2(K,[X1,X2,X3,X4,X5,X6,X7,X8,X9|Ys],G1s,G2s,G3s,G4s,G5s,
		(1-X1*X2)*X4*(exp(X6*(G1K-G2K-G3K*X7*1e-3+G4K*X9*1e-3))-1)-G5K*X1+G4K == 0
		) :-
		arg(K,G1s,G1K), arg(K,G2s,G2K), arg(K,G3s,G3K), arg(K,G4s,G4K), arg(K,G5s,G5K).
Due to slow convergence, just applying the contraints and using `solve` is fairly ineffective; the builtin throttling mechanism, even with a fairly high `clpBNR_iteration_limit`, prematurely terminates many of the sub-branches without failing so pruning is minimal. Using the Taylor contractor is a significant improvement but can still take many hours before producing a moderately acceptable result (~4 digits of precision in agreement with the published answer).
 
Through experimentation, it was discovered that it's better to split all variables at each iteration step rather than just splitting the largest interval, leading to:
eg
	﻿?- Vs=[X1,X2,X3,X4,X5,X6,X7,X8,X9],ebers_moll(Vs,Es),Vs::real(0,10),build_taylor_merged(Es,T),
	iterate_until(20,small(Vs),(T,maplist(mid_split,Vs))).
	Vs = [X1, X2, X3, X4, X5, X6, X7, X8, X9],
	Es = [(1-X1*X2)*X3*(exp(X5*(0.982-20.2153*X7*0.001-211.4823*X8*0.001))-1)-211.4823+191.267*X2==0,  (1-X1*X2)*X3*(exp(X5*(0.869-22.9274*X7*0.001-134.3844*X8*0.001))-1)-134.3844+111.461*X2==0,  (1-X1*X2)*X3*(exp(X5*(0.752-10.0677*X7*0.001-111.8468*X8*0.001))-1)-111.8468+101.779*X2==0,  (1-X1*X2)*X3*(exp(X5*(0.485-5.2095*X7*0.001-28.5132*X8*0.001))-1)-28.5132+23.3037*X2==0,  (1-X1*X2)*X4*(exp(X6*(0.982-1.455-20.2153*X7*0.001+191.267*X9*0.001))-1)-211.4823*X1+191.267==0,  (1-X1*X2)*X4*(exp(X6*(0.869-0.703-22.9274*X7*0.001+111.461*X9*0.001))-1)-134.3844*X1+111.461==0,  (1-X1*X2)*X4*(exp(X6*(0.752-1.254-10.0677*X7*0.001+101.779*X9*0.001))-1)-111.8468*X1+101.779==0,  (1-X1*X2)*X4*(exp(X6*(0.485-0.369-5.2095*X7*0.001+23.3037*X9*0.001))-1)-28.5132*X1+23.3037==0, X1*X3-X2*X4==0],
	T = cf_contractor([X9, X6, X4, X8, X7, X5, X3, X2, X1], [_A, _B, _C, _D, _E, _F, _G, _H, _I]),
	X1:: 0.8999987...,
	X2:: 0.450032...,
	X3:: 1.000169...,
	X4:: 2.00019...,
	X5:: 7.99964...,
	X6:: 8.00055...,
	X7:: 5.00083...,
	X8:: 0.99987...,
	X9:: 1.99956...,
	_I::real(-1.0Inf, 1.0Inf),
	_H::real(-1.0Inf, 1.0Inf),
	_G::real(-1.0Inf, 1.0Inf),
	_F::real(-1.0Inf, 1.0Inf),
	_E::real(-1.0Inf, 1.0Inf),
	_D::real(-1.0Inf, 1.0Inf),
	_C::real(-1.0Inf, 1.0Inf),
	_B::real(-1.0Inf, 1.0Inf),
	_A::real(-1.0Inf, 1.0Inf) .
This is just the first solution returned. Again, throttling prevents running fixed point iteration long enough to find failures that would prune the search tree caused by splitting. The complete answer would be the union of all such solutions. But the computation costs of either disabling throttling or collecting all solutions is probably prohibitive for this example, demonstrating the limitations of this implementation.

##### Summary

CISC based interval constraint systems overcame deficiencies in RISC, such as slow convergence, by building sophisticated narrowing algorithms into the constraint engine. But metalevel contractors can overcome some of these deficiencies without complicating the underlying engine design. Advances in hardware and software (Prolog) results in comparable performance to earlier CISC implementations, although the comparison is a bit unfair and CISC engine implemented in C will always be faster than this implementation of CLP(BNR) on equivalent hardware. The other advantage of this metalevel design is that it facilitates experimentation with new contractors without understanding (or compromising) the details of the underlying constraint system. This faciliates the addition of "specialized" contractors that may be more effective for particular problem domains.

<#TableOfContents>

&
	[Solving Polynomial Equations in one Variable] <- link #toc4Solving_Polynomial_Equations_in_one_Variable
	[Progress in the solving of a circuit design problem] <- link https://www.researchgate.net/publication/2322545_Progress_in_the_Solving_of_a_Circuit_Design_Problem
	[Algorithmic Power from Declarative Use of Redundant Constraints] <- link http://webhome.cs.uvic.ca/~vanemden/Publications/redund.pdf
	[Appendix 1] <- link #toc4Appendix_1_-__clpBNRmeta_contractor.pl__Source